apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.agilefreaks.gripit"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName

        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false

            applicationIdSuffix ".dev"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    flavorDimensions "default"

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "default"
        }
        local {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // App's dependencies
    implementation project(':domain')
    implementation project(':data')

    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:support-v13:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly "org.glassfish:javax.annotation:$javaxVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rxBindingVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "com.android.support.test.espresso:espresso-idling-resource:$espressoVersion"
    implementation "com.google.android:flexbox:$flexBoxVersion"
    implementation "com.github.chrisbanes:PhotoView:$photoViewVersion"
    implementation "de.hdodenhof:circleimageview:$circleViewVersion"
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"
    implementation "android.arch.persistence.room:rxjava2:$roomVersion"
    implementation "com.getbase:floatingactionbutton:$fabVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0", {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    testImplementation "android.arch.persistence.room:testing:$roomVersion", {
        exclude module: 'gson'
        exclude module: 'kotlin-stdlib'
    }

    // Espresso UI Testing
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espressoContribVersion") {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }

    androidTestImplementation("com.android.support.test.espresso:espresso-intents:$espressoIntent") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}