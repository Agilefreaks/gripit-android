apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.agilefreaks.gripit"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName

        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false

            applicationIdSuffix ".dev"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        local
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true);
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // App's dependencies
    compile project(':domain')
    compile project(':data')

    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:support-v13:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.android.databinding:compiler:$databindingVersion"
    provided "org.glassfish:javax.annotation:$javaxVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "io.reactivex.rxjava2:rxkotlin:$rxVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rxBindingVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    compile "com.squareup.picasso:picasso:$picassoVersion"
    compile "com.android.support.test.espresso:espresso-idling-resource:$espressoVersion"
    compile "com.google.android:flexbox:$flexBoxVersion"
    compile "com.github.chrisbanes:PhotoView:$photoViewVersion"
    compile "de.hdodenhof:circleimageview:$circleViewVersion"
    compile "android.arch.persistence.room:runtime:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"
    compile "android.arch.persistence.room:rxjava2:$roomVersion"
    compile "com.getbase:floatingactionbutton:$fabVersion"

    // Dependencies for local unit tests
    testCompile "junit:junit:$junitVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0", {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    testCompile "android.arch.persistence.room:testing:$roomVersion", {
        exclude module: 'gson'
        exclude module: 'kotlin-stdlib'
    }

    // Espresso UI Testing
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espressoContribVersion") {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}